PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
                        PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
                        prefix schema: <http://schema.org/>
                        prefix sschema: <https://schema.org/>
                        SELECT distinct ?subj ?pubname (GROUP_CONCAT(DISTINCT ?placename; SEPARATOR=", ") AS ?placenames)
        (GROUP_CONCAT(DISTINCT ?kwu; SEPARATOR=", ") AS ?kw)
        ?datep  (MIN(?url) as ?disurl) ?score  ?name ?description ?resourceType ?g
        WHERE {
            ?lit bds:search "${q}" .
            ?lit bds:matchAllTerms ${exact} .

            ?lit bds:relevance ?score .

            ?subj ?p ?lit .
                      ?subj schema:name|sschema:name ?name .
           graph ?g { ?subj schema:description|sschema:description ?description . }
            BIND (IF (exists {?subj a schema:Dataset .} ||exists{?subj a sschema:Dataset .} , "data", "tool") AS ?resourceType).

						Minus {?subj a sschema:ResearchProject } .
						Minus {?subj a schema:ResearchProject } .

						Minus {?subj a schema:Person } .
						Minus {?subj a sschema:Person } .

            optional {?subj sschema:distribution/sschema:url|sschema:subjectOf/sschema:url|sschema:url ?url .}
            OPTIONAL {?subj schema:datePublished|sschema:datePublished ?date_p .}
            OPTIONAL {?subj schema:publisher/schema:name|sschema:publisher/sschema:name ?pub_name .}
            OPTIONAL {?subj schema:spatialCoverage/schema:name|sschema:spatialCoverage/sschema:name|sschema:sdPublisher ?place_name .}
            OPTIONAL {?subj schema:keywords|sschema:keywords ?kwu .}
				    BIND ( IF ( BOUND(?date_p), ?date_p, "No datePublished") as ?datep ) .
		        BIND ( IF ( BOUND(?pub_name), ?pub_name, "No Publisher") as ?pubname ) .
            BIND ( IF ( BOUND(?place_name), ?place_name, "No spatialCoverage") as ?placename ) .
        }
        GROUP BY ?subj ?datep ?pubname ?name ?description  ?score ?resourceType ?g
        ORDER BY DESC(?score)
        LIMIT ${n}
        OFFSET ${o}
